* xsection.F
* routines to compute the cross-section
* this file is part of FormCalc
* last modified 26 May 08 th


#include "process.h"

#include MODEL

#ifdef BREMSSTRAHLUNG
#include "softphoton.F"
#endif

************************************************************************
** ProcessIni translates the polarization string into bit-encoded
** helicities, determines the averaging factor, and initializes the
** model defaults.

	subroutine ProcessIni(fail, pol,
     &    sqrtSfrom, sqrtSto, sqrtSstep)
	implicit none
	integer fail
	character*(*) pol
	double precision sqrtSfrom, sqrtSto, sqrtSstep

#include "xsection.h"

#if U77EXT
	integer lnblnk
	external lnblnk
#endif

	integer i, c, bits, df

#define SCALAR 0
#define FERMION 1
#define PHOTON 2
#define GLUON PHOTON
#define VECTOR 3

	integer type(LEGS)
	data type /TYPES/

	if( lnblnk(pol) .ne. LEGS ) then
	  fail = 1
	  return
	endif

	df = 2**(LEGS)

	do i = 1, LEGS
	  if( type(i) .eq. SCALAR ) then
	    c = ichar('S')
	    bits = 1
	  else
	    c = ichar(pol(i:i))
	    if( c .eq. ichar('+') ) then
	      bits = 4
	    else if( c .eq. ichar('-') ) then
	      bits = 1
	    else
	      c = iand(c, 223)
	      if( c .eq. ichar('U') ) then
	        bits = 7
	      else if( c .eq. ichar('T') ) then
	        bits = 5
	      else if( c .eq. ichar('L') ) then
	        bits = 2
	      else
	        bits = 0
	      endif

	      if( type(i) .ne. VECTOR ) then
	        bits = iand(bits, -3)
	        c = ior(c, 1)
	      endif

	      if( bits .eq. 0 ) then
	        WARN "Invalid polarization for leg ", Digit(i)
	        fail = 2
	        return
	      endif

	      if( i .le. LEGS_IN ) df = df*(ibits(bits, 0, 1) +
     &          ibits(bits, 1, 1) + ibits(bits, 2, 1))

#ifdef DIRACFERMIONS
	      if( type(i) .eq. FERMION ) then
	        df = df/2
	        bits = 2
	      endif
#endif
	    endif
	  endif

	  pol(i:i) = char(c)
	  helicities = helicities*8 + bits
	enddo

	Lower(SQRTS) = sqrtSfrom
	Upper(SQRTS) = sqrtSto
	Step(SQRTS) = sqrtSstep

	Var(FIXED) = 0
	Step(FIXED) = 1

	Var(TRIVIAL) = 0
	Step(TRIVIAL) = 0

	avgfac = 2**(LEGS)*DBLE(COLOURFACTOR)*DBLE(IDENTICALFACTOR)/df
	sqrtS = -1
	threshold = -1
	scale = -1
	sqrtSinvalid = 1

	call ffini
	call ModelDefaults
	call LumiDefaults

	fail = 0
	end


************************************************************************
** SetEnergy sets the energy for the partonic scattering process.
** All scale-dependent quantities are initialized at SCALE.

	subroutine SetEnergy(fail, newsqrtS)
	implicit none
	integer fail
	double precision newsqrtS

#include "xsection.h"

	double precision newmass_in, newmass_out, newscale

	if( newsqrtS .eq. sqrtS ) then
	  if( btest(flags, BIT_RESET) ) call ModelVarIni(fail, scale)
	  fail = 2*sqrtSinvalid
	  return
	endif

	fail = 0
	sqrtS = newsqrtS

1	newscale = max(DBLE(SCALE), 1D0)
	newmass_in = MASS_IN
	newmass_out = MASS_OUT

	if( abs(scale - newscale) +
     &      abs(mass_in - newmass_in) +
     &      abs(mass_out - newmass_out) .gt. 1D-9 ) then
	  mass_in = newmass_in
	  mass_out = newmass_out
	  scale = newscale
	  call ModelVarIni(fail, scale)
	  goto 1
	endif

	threshold = max(mass_in, mass_out)

	if( fail .ne. 0 .or. sqrtS .lt. threshold ) then
	  fail = 1
	else
	  call InitialState
	  flags = ibset(flags, BIT_RESET)
	endif

	sqrtSinvalid = fail
	end


************************************************************************
** ParameterScan performs the actual calculation.
** It scans over the parameter loops LOOP* declared in run.F.

	subroutine ParameterScan(dir,
     &    serialfrom, serialto, serialstep)
	implicit none
	character*(*) dir
	integer serialfrom, serialto, serialstep

#include "xsection.h"

	integer openlog
	external openlog

	integer serial, next

	next = serialfrom
	serial = 0

#ifdef LOOP1
	LOOP1
#endif
#ifdef LOOP2
	LOOP2
#endif
#ifdef LOOP3
	LOOP3
#endif
#ifdef LOOP4
	LOOP4
#endif
#ifdef LOOP5
	LOOP5
#endif
#ifdef LOOP6
	LOOP6
#endif
#ifdef LOOP7
	LOOP7
#endif
#ifdef LOOP8
	LOOP8
#endif
#ifdef LOOP9
	LOOP9
#endif
#ifdef LOOP10
	LOOP10
#endif
#ifdef LOOP11
	LOOP11
#endif
#ifdef LOOP12
	LOOP12
#endif
#ifdef LOOP13
	LOOP13
#endif
#ifdef LOOP14
	LOOP14
#endif
#ifdef LOOP15
	LOOP15
#endif
#ifdef LOOP16
	LOOP16
#endif
#ifdef LOOP17
	LOOP17
#endif
#ifdef LOOP18
	LOOP18
#endif
#ifdef LOOP19
	LOOP19
#endif
#ifdef LOOP20
	LOOP20
#endif

	serial = serial + 1
	if( serial .lt. next ) goto 1

	call flush(6)
	if( openlog(dir, serial) .eq. 0 ) then
	  call IntegratedCS
	  call flush(6)
	  call closelog
	endif

	next = next + serialstep
	if( next .gt. serialto ) return

1	continue
	end


************************************************************************
** IntegratedCS computes the integrated cross-section at one point in
** parameter space.  The cross-section is differential in all variables
** with non-zero step, i.e. integration variables have step = 0.

	subroutine IntegratedCS
	implicit none

#include "xsection.h"

	integer nvars
	parameter (nvars = MAXVAR - (MINVAR) + 1)

	external DifferentialCS

	double precision result(NCOMP), error(NCOMP), show(nvars)
	integer fail, f, v, nfix, ndim, fix(nvars)

	call ModelConstIni(fail)
	if( fail .ne. 0 ) goto 999

	flags = 2**BIT_RESET + 2**BIT_LOOP

#ifdef MUDIM
	call setmudim(DBLE(MUDIM))
#endif
#ifdef DELTA
	call setdelta(DBLE(DELTA))
#endif
#ifdef LAMBDA
	call setlambda(DBLE(LAMBDA))
#endif
	Divergence = getdelta()

	call KinIni(fail)
	if( fail .ne. 0 ) goto 999

	nfix = 0
	do v = MINVAR, MAXVAR
	  if( Step(v) .ne. 0 ) then
	    nfix = nfix + 1
	    fix(nfix) = v
	    Var(v) = Lower(v)
	  endif
	enddo

	ndim = nvars - nfix

	call LumiIni(fail)
	if( fail .ne. 0 ) goto 999

#define SHOW print 100,
100	format("|# ", A, "=", F10.4, SP, F10.4, " I")

#ifdef MMA
	call MmaSetPara
#endif

#ifdef PRINT1
	PRINT1
#endif
#ifdef PRINT2
	PRINT2
#endif
#ifdef PRINT3
	PRINT3
#endif
#ifdef PRINT4
	PRINT4
#endif
#ifdef PRINT5
	PRINT5
#endif
#ifdef PRINT6
	PRINT6
#endif
#ifdef PRINT7
	PRINT7
#endif
#ifdef PRINT8
	PRINT8
#endif
#ifdef PRINT9
	PRINT9
#endif
#ifdef PRINT10
	PRINT10
#endif
#ifdef PRINT11
	PRINT11
#endif
#ifdef PRINT12
	PRINT12
#endif
#ifdef PRINT13
	PRINT13
#endif
#ifdef PRINT14
	PRINT14
#endif
#ifdef PRINT15
	PRINT15
#endif
#ifdef PRINT16
	PRINT16
#endif
#ifdef PRINT17
	PRINT17
#endif
#ifdef PRINT18
	PRINT18
#endif
#ifdef PRINT19
	PRINT19
#endif
#ifdef PRINT20
	PRINT20
#endif

#ifdef MMA
	call MmaEndSet
	call MmaSetData
#endif

1	call Cuba(ndim, DifferentialCS, result, error)

	do f = 1, nfix
	  show(f) = Show(fix(f))
	enddo

#ifdef MMA
	call MmaData(show, nfix, result, error, NCOMP)
#else
* Note: "real" data lines are tagged with "|" in the output.
101	format("| ", 10(4G19.10, :, /"|+"))
	print 101, (show(f), f = 1, nfix)

102	format("|+   ", NCOMP G24.15)
	print 102, result
	print 102, error

	call flush(6)
#endif

	do f = nfix, 1, -1
	  v = fix(f)
	  Var(v) = Var(v) + Step(v)
	  if( (Var(v) - Upper(v))/Step(v) .lt. 1D-10 ) goto 1
	  Var(v) = Lower(v)
	enddo

999	continue

#ifdef MMA
	call MmaEndSet
#else
103	format("|"/"|"/)
	print 103
#endif
	end


************************************************************************
** DifferentialCS computes the differential cross-section at x.
** For all integration variables (those with zero step) it factors in
** the Jacobian, too.

	subroutine DifferentialCS(ndim, x, ncomp, result)
	implicit none
	integer ndim, ncomp
	double precision x(ndim), result(ncomp)

#include "xsection.h"

#ifdef BREMSSTRAHLUNG
	double precision SoftPhotonFactor
	external SoftPhotonFactor
#endif

	double precision fac, range, flux
	integer v, d, c

	fac = avgfac
	d = 0
	do v = MINVAR, MAXVAR
	  if( Step(v) .eq. 0 ) then
	    range = Upper(v) - Lower(v)
	    d = d + 1
	    Var(v) = Lower(v) + x(d)*range
	    fac = fac*range
	  endif
	  Show(v) = Var(v)
	enddo

	do c = 1, ncomp
	  result(c) = 0
	enddo

	call Luminosity(fac)
	if( fac .eq. 0 ) return

	call FinalState(fac)
	if( fac .eq. 0 ) return

	if( btest(flags, BIT_RESET) ) then
	  call clearcache
#ifndef NO_RENCONST
	  call CalcRenConst
#endif
	endif

	call SquaredME(result, helicities, flags)

	flags = ibclr(flags, BIT_RESET)

#if LEGS_IN == 1
	flux = 2*sqrtS
#else
	flux = 4/hbar_c2*sqrtS*momspec(SPEC_K, 1)
#endif
	fac = fac/((2*pi)**(3*LEGS_OUT - 4)*2*sqrtS*flux)

	do c = 1, ncomp
	  if( .not. abs(result(c)) .lt. 1D12 ) then
	    WARN "Got strange values from SquaredME:"
	    WARN result
	    WARN "(Did you compute the colour matrix elements?)"
	    stop
	  endif
	  result(c) = result(c)*fac
	enddo

#ifdef BREMSSTRAHLUNG
	result(2) = result(2) + SoftPhotonFactor()*result(1)
#endif

#ifdef WF_RENORMALIZATION
	result(2) = result(2) + (WF_RENORMALIZATION)*result(1)
#endif
	end


************************************************************************
** Cuba is a chooser for the Cuba routines, with special cases
** for ndim = 0 (integrand evaluation) and ndim = 1 (Patterson
** integration).

	subroutine Cuba(ndim, integrand, result, error)
	implicit none
	integer ndim
	external integrand
	double precision result(NCOMP), error(NCOMP)

	integer nregions, neval, fail, c
	double precision prob(NCOMP)

#define GAUSS 1
#define PATTERSON 2
#define VEGAS 3
#define SUAVE 4
#define DIVONNE 5
#define CUHRE 6

#define FLAGS_LAST 4
#define FLAGS_PSEUDO 8

	if( ndim .eq. 0 ) then

	  call integrand(0, 0D0, NCOMP, result)
	  do c = 1, NCOMP
	    error(c) = 0
	  enddo
	  return

	else if( ndim .eq. 1 ) then

#if METHOD == GAUSS

	  neval = 32
	  call Gauss(NCOMP, 0D0, 1D0, integrand,
     &      neval, result)
	  do c = 1, NCOMP
	    error(c) = -1
	    prob(c) = -1
	  enddo
	  nregions = 1
	  fail = 0
	  INFO "Gauss integration results:"

#else

	  call Patterson(NCOMP, 0D0, 1D0, integrand,
     &      DBLE(RELACCURACY), DBLE(ABSACCURACY),
     &      neval, fail, result, error)
	  do c = 1, NCOMP
	    prob(c) = -1
	  enddo
	  nregions = 1
	  INFO "Patterson integration results:"

#endif

	else

#if METHOD == VEGAS

	  call vegas(ndim, NCOMP, integrand,
     &      DBLE(RELACCURACY), DBLE(ABSACCURACY),
     &      VERBOSE, MINEVAL, MAXEVAL,
     &      NSTART, NINCREASE,
     &      neval, fail, result, error, prob)
	  nregions = 1
	  INFO "Vegas integration results:"

#elif METHOD == SUAVE

	  call suave(ndim, NCOMP, integrand,
     &      DBLE(RELACCURACY), DBLE(ABSACCURACY),
     &      VERBOSE + FLAGS_LAST, MINEVAL, MAXEVAL,
     &      NNEW, DBLE(FLATNESS),
     &      nregions, neval, fail, result, error, prob)
	  INFO "Suave integration results:"

#elif METHOD == DIVONNE

	  call divonne(ndim, NCOMP, integrand,
     &      DBLE(RELACCURACY), DBLE(ABSACCURACY),
     &      VERBOSE, MINEVAL, MAXEVAL,
     &      KEY1, KEY2, KEY3, MAXPASS,
     &      DBLE(BORDER), DBLE(MAXCHISQ), DBLE(MINDEVIATION),
     &      0, NDIM, 0, 0, 0,
     &      nregions, neval, fail, result, error, prob)
	  INFO "Divonne integration results:"

#else

	  call cuhre(ndim, NCOMP, integrand,
     &      DBLE(RELACCURACY), DBLE(ABSACCURACY),
     &      VERBOSE + FLAGS_LAST, MINEVAL, MAXEVAL,
     &      KEY,
     &      nregions, neval, fail, result, error, prob)
	  INFO "Cuhre integration results:"

#endif

	endif

	INFO "nregions =", nregions
	INFO "neval    =", neval
	INFO "fail     =", fail

	if( fail .gt. 0 ) then
	  WARN "Failed to reach the desired accuracy."
	else if( fail .lt. 0 ) then
	  WARN "Integration error."
	endif

100	format(I2, G24.15, " +- ", G24.15, "    p = ", F6.3)
	print 100, (c, result(c), error(c), prob(c), c = 1, NCOMP)
	end

